entrypoints ModuleDef;

Module . ModuleDef ::= "module" QName ";" [OpenModule] DefList;
ImportModule . OpenModule ::= "open" QName;
ImportModuleAs . OpenModule ::= "open" QName "as" SimpleName;
terminator OpenModule ";";

EmptyDef . DefList ::= ;
ConsTypeDef  . DefList ::= ADType ";" DefList;
ConsFunDef  . DefList ::= FunDef ";" DefList;
Mutual . DefList ::= "mutual" "{" DefList "}";

comment "--" ;
comment "{-" "-}" ;

QIdent . QName ::= [Ident];
separator nonempty Ident "." ;

SimpleIdent . SimpleName ::= Ident;

separator nonempty TypeExpr2 "";

TyInt   . TypeExpr2 ::= "Int";
TyFloat . TypeExpr2 ::= "Double";
TyStr   . TypeExpr2 ::= "String";
TyQName    . TypeExpr2 ::= QName;
TyApp      . TypeExpr1 ::= TypeExpr2 [TypeExpr2];
TyArr      . TypeExpr ::= TypeExpr1 "->" TypeExpr;
TyNamedArr . TypeExpr ::= "(" SimpleName ":" TypeExpr ")" "->" TypeExpr;

coercions TypeExpr 2;

UnionType . ADType ::= "type" SimpleName TypeParamList "=" ConstructorDeclList;

EmptyTyP  . TypeParamList ::= ;
LastTyP   . TypeParamList ::= Ident;
ConsTyP   . TypeParamList ::= Ident TypeParamList;

NilCtrD   . ConstructorDeclList ::=;
ConsCtrD  . ConstructorDeclList ::= "|" ConstructorDecl ConstructorDeclList;
CtrDecl   . ConstructorDecl ::= SimpleName ConstructorTypeDecl;

EmptyCtrTyD . ConstructorTypeDecl ::= ;
FullCtrTyD  . ConstructorTypeDecl ::= ":" TypeExpr;
ParamCtrTyD . ConstructorTypeDecl ::= "of" ParamTypeDecl;

LastParamTy . ParamTypeDecl ::= TypeExpr;
ConsParamTy . ParamTypeDecl ::= TypeExpr "*" ParamTypeDecl;

separator NamedParamDecl ";";

FullTyDecl  . NamedParamDecl ::= SimpleName ":" TypeExpr;
ParamTyDecl .NamedParamDecl ::= SimpleName "of" ParamTypeDecl;

RecordType . ADType ::= "type" SimpleName TypeParamList "=" SimpleName "{" [NamedParamDecl] "}";

CoType . ADType ::= "cotype" SimpleName TypeParamList "=" SimpleName "{" [NamedParamDecl] "}";

terminator FunParamDecl "";
SimplePDecl . FunParamDecl ::= SimpleName;
NamedPDecl  . FunParamDecl ::= "(" SimpleName ":" TypeExpr ")";

IdentPat . SinglePattern ::= QName;
AppSimplePat  . SinglePattern ::= SinglePattern SimpleName;
AppComplexPat . SinglePattern ::= SinglePattern "(" SinglePattern ")";
AppAtPat . SinglePattern ::= SinglePattern "(" SinglePattern ")" "@" SimpleName;
AtPat    . SinglePattern ::= "(" SinglePattern ")" "@" SimpleName;

separator nonempty SinglePattern ",";
--LastPat . PatternList ::= SinglePattern;
--ConsPat . PatternList ::= SinglePattern "," PatternList;

MatchBranch . Branching ::= "|" [SinglePattern] "->" TermExpr ;
terminator nonempty Branching ";";

RecFieldBind . FieldBinding ::= QName "=" TermExpr;
separator nonempty FieldBinding ";";


TermOr. TermExpr2 ::= TermExpr2 "|" TermExpr3;
TermAnd. TermExpr3 ::= TermExpr3 "&" TermExpr4;

TermEqual. TermExpr4 ::= TermExpr4 "=" TermExpr5;
TermInEqual. TermExpr4 ::= TermExpr4 "!=" TermExpr5;

TermLess. TermExpr5 ::= TermExpr5 "<" TermExpr6;
TermLessEq. TermExpr5 ::= TermExpr5 "<=" TermExpr6;
TermGt. TermExpr5 ::= TermExpr5 ">" TermExpr6;
TermGtEq. TermExpr5 ::= TermExpr5 ">=" TermExpr6;

TermAdd. TermExpr6  ::= TermExpr6 "+" TermExpr7;
TermSub. TermExpr6  ::= TermExpr6 "-" TermExpr7;

TermMul. TermExpr7 ::= TermExpr7 "*" TermExpr8;
TermDiv. TermExpr7 ::= TermExpr7 "/" TermExpr8;
TermRem. TermExpr7 ::= TermExpr7 "%" TermExpr8;

TermNot. TermExpr8 ::= "!" TermExpr9;
TermNeg. TermExpr8 ::= "(" "-" TermExpr9 ")";

separator nonempty TermExpr10 "";
TermApp   . TermExpr9 ::= TermExpr10 [TermExpr10];

ConstInt. TermExpr10 ::= Integer ;
ConstDouble. TermExpr10 ::= Double ;
ConstStr. TermExpr10 ::= String ;
TermIdent . TermExpr10 ::= QName;

TermConvInt . TermExpr10 ::= "int" "(" TermExpr ")";
TermConvStr . TermExpr10 ::= "string" "(" TermExpr ")";
TermConvDouble . TermExpr10 ::= "double" "(" TermExpr ")";

TermAbs   . TermExpr ::= "fun" FunParamDecl [FunParamDecl] "=>" TermExpr;
TermBindPat . TermExpr ::= "let" "(" SinglePattern ")" "=" TermExpr "in" TermExpr;
TermBind  . TermExpr ::= "let" SimpleName [FunParamDecl] "=" TermExpr "in" TermExpr;
TermBindRec . TermExpr ::= "let" "rec" SimpleName [FunParamDecl] "=" TermExpr "in" TermExpr;
TermProj . TermExpr ::= "(" TermExpr ")" "^" QName;
TermWithType . TermExpr ::= "(" TermExpr ":" TypeExpr ")";
TermRecMod . TermExpr ::= "{" TermExpr "with" [FieldBinding] "}";
TermPatMatch . TermExpr ::= "match" TermExprList "with" "{" [Branching] "}";
TermImpoMatch . TermExpr ::= "match" TermExprList "with" "absurd";
coercions TermExpr 10;

LastTerm . TermExprList ::= TermExpr;
ConsTerm . TermExprList ::= TermExpr "," TermExprList;

FunBind      . FunDef ::= "let" SimpleName [FunParamDecl] "=" TermExpr;
FunBindWithR . FunDef ::= "let" SimpleName [FunParamDecl] ":" TypeExpr "=" TermExpr;
FunBindRec      . FunDef ::= "let" "rec" SimpleName [FunParamDecl] "=" TermExpr;
FunBindRecWithR . FunDef ::= "let" "rec" SimpleName [FunParamDecl] ":" TypeExpr "=" TermExpr;